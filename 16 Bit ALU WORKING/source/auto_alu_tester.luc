// Logic module for automatic tester for 16bit ALU:
// takes in INPUT_A, INPUT_B, OPCODE and EXPECTED to feed one specified test case into the alu
// output from ALU is stored in d flip-flop called 'out'

// We use an FSM to change the display on the seven segment of the FPGA to show the 
// input value of A, input value of B, expected output from ALU and actual output from ALU 
// which are shown in states A_VALUE, B_VALUE, EXPECT and OUT respectively
// if the output from the ALU is equal to the expected value fed into the test case's parameters,
// we transition of state PASS, and the letter 'P' is shown
// else, we transition to state FAIL, and the letter 'F' is shown

// This logic module links up with the auto_tester_run module, together forming the auto tester for our ALU
// Take note of the output 'done', of this module, which triggers the next test case to run in the run module,
// which you can see later in the auto_tester_run lucid file.

module auto_alu_tester
#(
    INPUT_A,
    INPUT_B,
    OPCODE,
    EXPECTED
  )
(
    input clk,  // clock : connect to regular 100MHz clock for Alchitry FPGA module
    input rst,  // reset
    input start,
    output io_seg[8],
    output io_sel[4],
    output opcode_led[6],
    output done
  ) {
  
  //edge_detector timerClock(#RISE(1), #FALL(0), .clk(clk));
  
  //fsm state(.clk(clk), .rst(rst)) = {INIT, A_VALUE, B_VALUE, EXPECT, OUT, PASS, FAIL};
  //dff out[16](#INIT(0), .clk(clk), .rst(rst));
  //counter timer (#SIZE(1), #DIV(25), .clk(clk), .rst(rst)); // pace of transition from one value to the next displayed
 // multi_seven_seg seg_display(.clk(clk), .rst(rst));
  // same ways of doing the same thing..
  
    .clk(clk) {
    edge_detector timerClock(#RISE(1), #FALL(0));
    
    .rst(rst){
      fsm state = {INIT, A_VALUE, B_VALUE, EXPECT, OUT, PASS, FAIL};
      dff out[16];
      counter timer(#SIZE(1), #DIV(25));
      multi_seven_seg seg_display;
    }
  }
  
  sig alufn[6];
  sig a[16];
  sig b[16];
  sig expected[16];
  
  // instantiate ALU
  alu_16_bit alu(.alufn(alufn), .a(a), .b(b));

  always {

    done = 0;
    timerClock.in = timer.value;
    io_seg = ~seg_display.seg; // connect segments to the driver
    io_sel = ~seg_display.sel; // connect digit select to the driver
    // set initial display values
    seg_display.values = {4b0000,4b0000,4b0000,4b0000};
    
    alufn = OPCODE;
    a = INPUT_A;
    b = INPUT_B;
    expected = EXPECTED;
    out.d = alu.out;
    
    opcode_led = OPCODE;
    
    case (state.q){
      state.INIT:
        if (start==1)
          state.d = state.A_VALUE;
   
      state.A_VALUE:
        seg_display.values = {a[15:12], a[11:8], a[7:4], a[3:0]};
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;
        if (timerClock.out==1)
          state.d = state.B_VALUE;
      
        
      state.B_VALUE:
        seg_display.values = {b[15:12], b[11:8], b[7:4], b[3:0]};
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;
        if (timerClock.out==1)
          state.d = state.EXPECT;
          
      state.EXPECT:
        // display expected values
        seg_display.values = {expected[15:12], expected[11:8], expected[7:4], expected[3:0]};
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;
        if (timerClock.out==1)
          state.d = state.OUT;
       
      state.OUT:
        // display actual output values from the ALU based on input given
        seg_display.values = {out.q[15:12], out.q[11:8], out.q[7:4], out.q[3:0]};
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;
        
        if (timerClock.out && out.q == EXPECTED)
          state.d = state.PASS;
        else if (timerClock.out && out.q != EXPECTED)  
          state.d = state.FAIL;
          
      state.PASS:
        io_seg = 8b10001100; // displays the letter 'P'
        io_sel = 4b1110;     // on the first digit of the seven segment display
        opcode_led = 6b000000;
        // value of output done is changed from 0 initially to 1, to trigger next test case to run
        // in the auto_tester_run file.
        done = 1;
        
      state.FAIL:
        io_seg = 8b10001110; // displays the letter 'F'
        io_sel = 4b1110;     // on the first digit
        
      }



  }
}
